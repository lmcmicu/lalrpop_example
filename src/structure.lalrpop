use std::str::FromStr;
use ast::AstExpression;


grammar;

pub Expression: Box<AstExpression> = {
    Label => Box::new(AstExpression::Label(<>)),
    Function,
};

Label = {
    ALPHANUM,
    DQSTRING,
    SQSTRING,
};

ALPHANUM: String = r"[a-zA-Z0-9-_]+" => String::from(<>);
DQSTRING: String = r#""([^"]|\\")*""# => String::from(<>);
SQSTRING: String = r#"'([^']|\\')*'"# => String::from(<>);

Function: Box<AstExpression> = <f:FunctionName> "(" <a:Arguments> ")"
    => Box::new(AstExpression::Function(f, a));

FunctionName = ALPHANUM;
Arguments = Comma<Argument>;
Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T> => {
        v.push(e);
        v
    }
};

Argument: Box<AstExpression> = {
    Label => Box::new(AstExpression::Label(<>)),
    Field,
    NamedArg,
    RegexMatch,
    RegexSub,
    Function,
}

Field: Box<AstExpression> = <l1:Label> "." <l2:Label> => Box::new(AstExpression::Field(l1, l2));
NamedArg: Box<AstExpression> = <l1:Label> "=" <l2:Label> => Box::new(AstExpression::NamedArg(l1, l2));

RegexMatch: Box<AstExpression> = r"/(([^\\/]*(\\/)*[^\\/]*)+)/([a-z]*)" => {
    let fullmatch = String::from(<>);
    let re = regex::Regex::new(r"/(([^\\/]*(\\/)*[^\\/]*)+)/([a-z]*)").unwrap();
    let caps = re.captures(&fullmatch).unwrap();
    let pattern = caps.get(1).unwrap().as_str().to_string();
    let flags = caps.get(4).unwrap().as_str().to_string();
    Box::new(AstExpression::RegexMatch(pattern, flags))
};

RegexSub: Box<AstExpression> = r"s/(([^\\/]*(\\/)*[^\\/]*)+)/(([^\\/]*(\\/)*[^\\/]*)+)/([a-z]*)" => {
    let fullmatch = String::from(<>);
    let re = regex::Regex::new(r"s/(([^\\/]*(\\/)*[^\\/]*)+)/(([^\\/]*(\\/)*[^\\/]*)+)/([a-z]*)")
        .unwrap();
    let caps = re.captures(&fullmatch).unwrap();
    let pattern = caps.get(1).unwrap().as_str().to_string();
    let replacement = caps.get(4).unwrap().as_str().to_string();
    let flags = caps.get(7).unwrap().as_str().to_string();
    Box::new(AstExpression::RegexSub(pattern, replacement, flags))
};
